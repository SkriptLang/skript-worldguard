import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group 'org.skriptlang.skript'
version '1.0.0'

compileJava.options.encoding = 'UTF-8'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.skriptlang.org/releases' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url "https://maven.enginehub.org/repo/" }
}

dependencies {
    // eclipse annotations are only present for IDE analysis, they should not be used
    implementation group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.19.4-R0.1-SNAPSHOT'
    implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.12.0-pre2') {
        transitive = false
    }
    implementation (group: 'com.sk89q.worldguard', name: 'worldguard-bukkit', version: '7.0.8') {
        exclude module: 'bstats-bukkit'
    }
}

processResources {
    filter ReplaceTokens, tokens: [
        'version': version
    ]
}

tasks.register('nightlyResources', ProcessResources) {
    from 'src/main/resources', {
        include '**'
        filter ReplaceTokens, tokens: [
                'version': project.property('version') + '-nightly-' + 'git rev-parse --short HEAD'.execute().text.trim()
        ]
    }
    into 'build/resources/main'
}

tasks.register('nightlyBuild', Jar) {
    dependsOn nightlyResources
    from sourceSets.main.output
    archiveFileName = 'skript-worldguard-nightly.jar'
}
